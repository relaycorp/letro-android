plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
    id 'org.jlleitschuh.gradle.ktlint' version '11.6.1'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'tech.relaycorp.letro'
    compileSdk 34

    defaultConfig {
        applicationId "tech.relaycorp.letro"
        minSdk 23
        targetSdk 34
        versionCode 1
        versionName project.findProperty("versionName") ?: "0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '17'
        allWarningsAsErrors = true
    }
    buildFeatures {
        buildConfig true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.3'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    lint {
        abortOnError true
        checkAllWarnings true
        lintConfig file('lint.xml')
        warningsAsErrors true
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.9.21')

    // AndroidX
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.sharetarget:sharetarget:1.2.0'

    // Awala-powered messaging
    implementation 'com.github.relaycorp:awala-endpoint-android:1.15.0'
    def bouncy_castle_version = "1.70"
    implementation "org.bouncycastle:bcprov-jdk15on:$bouncy_castle_version"
    implementation "org.bouncycastle:bcpkix-jdk15on:$bouncy_castle_version"

    // VeraId
    implementation 'tech.relaycorp:veraid:1.11.5'

    // Compose
    implementation platform('androidx.compose:compose-bom:2023.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.31.5-beta'

    // Compose related
    implementation 'androidx.navigation:navigation-compose:2.7.4'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation 'androidx.activity:activity-compose:1.7.2'

    // Encryption:
    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha06'
    implementation "net.zetetic:android-database-sqlcipher:4.5.4"

    // Hilt
    def hilt_version = "2.48.1"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Room
    def room_version = "2.5.2"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    // Utils:
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    implementation 'com.google.code.gson:gson:2.10.1'

    def accompanist_permissions_version = "0.32.0"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_permissions_version"

    implementation "com.airbnb.android:lottie-compose:6.2.0"

    // Firebase:
    implementation(platform("com.google.firebase:firebase-bom:32.4.1"))
    implementation("com.google.firebase:firebase-crashlytics-ktx")
    implementation("com.google.firebase:firebase-analytics-ktx")

    // Testing
    def junitVersion = "5.10.1"
    def kotlinCoroutinesVersion = "1.7.3"
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2023.06.01')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    testImplementation 'io.kotest:kotest-assertions-core:5.8.0'
    testImplementation "io.mockk:mockk:1.13.8"
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion")
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}
ksp {
    arg('room.schemaLocation', "$projectDir/schemas")
}

ktlint {
    version.set("0.49.1")
}
