plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
    id 'org.jlleitschuh.gradle.ktlint' version '11.6.0'
    id 'kotlin-parcelize'
}

android {
    namespace 'tech.relaycorp.letro'
    compileSdk 34

    defaultConfig {
        applicationId "tech.relaycorp.letro"
        minSdk 23
        targetSdk 34
        versionCode 1
        versionName project.findProperty("versionName") ?: "0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '17'
        allWarningsAsErrors = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.3'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    lint {
        abortOnError true
        checkAllWarnings true
        lintConfig file('lint.xml')
        warningsAsErrors true
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.22')

    // AndroidX
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'

    // Awala-powered messaging
    implementation 'com.github.relaycorp:awala-endpoint-android:1.14.0'
    def bouncy_castle_version = "1.70"
    implementation "org.bouncycastle:bcprov-jdk15on:$bouncy_castle_version"
    implementation "org.bouncycastle:bcpkix-jdk15on:$bouncy_castle_version"

    // VeraId
    implementation 'tech.relaycorp:veraid:1.10.0'

    // Compose
    implementation platform('androidx.compose:compose-bom:2023.09.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.31.5-beta'

    // Compose related
    implementation 'androidx.navigation:navigation-compose:2.6.0'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation 'androidx.activity:activity-compose:1.7.2'

    // Hilt
    def hilt_version = "2.47"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Room
    def room_version = "2.5.2"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    // Utils:
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    implementation 'com.google.code.gson:gson:2.10.1' // TODO: remove
    implementation "com.google.accompanist:accompanist-permissions:0.33.2-alpha"


    // Testing
    def junitVersion = "5.10.0"
    def kotlinCoroutinesVersion = "1.7.3"
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2023.06.01')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    testImplementation 'io.kotest:kotest-assertions-core:5.7.2'
    testImplementation "io.mockk:mockk:1.13.7"
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion")
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}
ksp {
    arg('room.schemaLocation', "$projectDir/schemas")
}

ktlint {
    version.set("0.49.1")
}
